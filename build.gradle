// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.3.61'
    ext.android_gradle_version = '3.5.3'
    ext.green_dao_version = '3.2.2'
    ext.FIELD_LOG_FILE = "LOG_FILE"
    ext.FIELD_LOG_CONSOLE = "LOG_CONSOLE"
    ext.KEY_LOG_FILE = "logToFile"
    ext.KEY_LOG_CONSOLE = "logToConsole"

    final commonProp = new Properties()
    commonProp.load(new FileInputStream(new File(rootDir, "common.properties")))
    println("common.properties:\n$commonProp")
    ext.commonProp = commonProp

    ext.buildInfo = [
            'compileSdkVersion': Integer.parseInt(commonProp['compileSdkVersion']),
            'buildToolsVersion': commonProp['buildToolsVersion'],
            'versionCode'      : Integer.parseInt(commonProp['versionCode']),
            'versionName'      : commonProp['versionName'],
            'targetSdkVersion' : Integer.parseInt(commonProp['targetSdkVersion']),
            'minSdkVersion'    : Integer.parseInt(commonProp['minSdkVersion'])
    ]

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$android_gradle_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.lyc:app-inject-plugin:latest.release"
        classpath "org.greenrobot:greendao-gradle-plugin:$green_dao_version"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url 'https://jitpack.io' }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
