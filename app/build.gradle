apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: "com.lyc.app-inject-plugin"

def showHomeBottomBar = (commonProp['showHomeBottomBar'] == "true")
println("showHomeBottomBar=${showHomeBottomBar}")

android {
    compileSdkVersion buildInfo['compileSdkVersion']
    buildToolsVersion buildInfo['buildToolsVersion']

    defaultConfig {
        applicationId "com.lyc.easyreader"
        minSdkVersion buildInfo['minSdkVersion']
        targetSdkVersion buildInfo['targetSdkVersion']
        versionCode buildInfo['versionCode']
        versionName buildInfo['versionName']
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField("Boolean", "SHOW_HOME_BOTTOM_BAR", showHomeBottomBar.toString())
    }

    signingConfigs {
        common {
            Properties prop = new Properties()
            prop.load(new FileInputStream(new File(rootDir, "private.properties")))
            println prop
            File storeFile = new File(getRootDir(), "reader_key.jks")
            setStoreFile(storeFile)
            setStorePassword(prop['sign_store_password'])
            setKeyPassword(prop['sign_key_password'])
            setKeyAlias(prop['sign_key_alias'])
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.common
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.common
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(":base")
    implementation project(":api")
    implementation 'com.lyc:app-inject:latest.release'
    if (showHomeBottomBar) {
        implementation project(":usercenter")
        implementation project(":discover")
    }
    implementation project(":bookshelf")
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
